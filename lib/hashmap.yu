import sys.stdlib
import sys.string

// key-value pair
public struct __KeyValue {
  key: u32,
  value: u8 var*,
  next: __KeyValue var*,
}

// table of pairs
public struct __KvPairTable {
  table: __KeyValue var* var*,
  size: usize,
  // number of non-null elements in table
  occupied: usize,
}

// table iterator
public struct __KvPairTableIter {
  table: __KvPairTable*,
  cur_index: usize,
  cur_pair: __KeyValue var*,
}

// hash map (dynamic table size)
public struct HashMap {
  __table: __KvPairTable,
  // size of hash map
  __size: usize,
  // hash function
  __hash_func: (HashMap var&, u32): usize,
}

// hash function
public type HashFunc = (HashMap var&, u32): usize


/*
 * key-value pair related implementations
 */
inline def key(this: __KeyValue*): u32 {
  (*this).key
}

inline def value(this: __KeyValue*): u8 var* {
  (*this).value
}


/*
 * iterator related implementations
 */
// set 'cur_index' and 'cur_pair' to next avaliable index
// which means 'table[cur_index]' is not null
def findNextIndex(this: __KvPairTableIter var&) {
  while this.cur_index < (*this.table).size {
    if (*this.table).table[this.cur_index] != null as __KeyValue var* {
      this.cur_pair = (*this.table).table[this.cur_index]
      break
    }
    this.cur_index += 1 as usize
  }
}

// iterator method 'next'
public def next(this: __KvPairTableIter var&): __KeyValue var* {
  let cur = this.cur_pair
  // find next key-value pair
  this.cur_pair = (*this.cur_pair).next
  if this.cur_pair == null as __KeyValue var* {
    this.cur_index += 1 as usize
    this.findNextIndex()
  }
  cur
}

// iterator method 'last'
inline def last(this: __KvPairTableIter&): bool {
  this.cur_index >= (*this.table).size
}


/*
 * table related implementations
 */
// internal constructor
def newTable(size: usize): __KvPairTable {
  let mem_size = size * sizeof __KeyValue var*
  let table = malloc(mem_size) as __KeyValue var* var*
  memset(table as u8 var*, 0, mem_size)
  [__KvPairTable] {table, size, 0 as usize}
}

// internal destructor
def del(this: __KvPairTable var&) {
  var i = 0
  while i as usize < this.size {
    var cur = this.table[i]
    while cur != null as __KeyValue var* {
      let next = (*cur).next
      free(cur as u8*)
      cur = next
    }
    i += 1
  }
  free(this.table as u8*)
}

// insert existing pair to table
def insert(this: __KvPairTable var&, index: usize, pair: __KeyValue var*) {
  if this.table[index] == null as __KeyValue var* {
    this.occupied += 1 as usize
  }
  (*pair).next = this.table[index]
  this.table[index] = pair
}

// insert key-value pair to table
def insert(this: __KvPairTable var&, index: usize,
           key: u32, value: u8 var*) {
  // allocate a new key-value pair
  let pair = malloc(sizeof __KeyValue) as __KeyValue var*
  (*pair).key = key
  (*pair).value = value
  // insert to table
  this.insert(index, pair)
}

// get value by key
def get(this: __KvPairTable&, index: usize, key: u32): u8 var* {
  var cur = this.table[index]
  while cur != null as __KeyValue var* {
    if (*cur).key == key {
      return (*cur).value
    }
    cur = (*cur).next
  }
  null as u8 var*
}

// remove value by key
def remove(this: __KvPairTable var&, index: usize, key: u32): bool {
  var cur = this.table[index], last = this.table + index
  while cur != null as __KeyValue var* {
    if (*cur).key == key {
      (*last) = (*cur).next
      free(cur as u8*)
      if this.table[index] == null as __KeyValue var* {
        this.occupied -= 1 as usize
      }
      return true
    }
    last = &((*cur).next)
    cur = (*cur).next
  }
  false
}

// get iterator
def iter(this: __KvPairTable&): __KvPairTableIter {
  var iter = [__KvPairTableIter]
             {&this, 0 as usize, null as __KeyValue var*}
  iter.findNextIndex()
  iter
}


/*
 * hash map related implementations
 */
// default table size
let TABLE_SIZE = 32 as usize

// default hash function
def defaultHasher(this: HashMap var&, key: u32): usize {
  (key as usize) * (0x9e3779b97f4a7c15 as usize)
}

// call hash function and returns table index
def hash(this: HashMap var&, key: u32): usize {
  (this.__hash_func)(this, key) % this.__table.size
}

// rehash when table is full, otherwise do nothing
// returns true if rehash is performed
def rehash(this: HashMap var&): bool {
  if this.__table.occupied == this.__table.size {
    // allocate new table
    let size = this.__table.size * 2 as usize
    var table = newTable(size)
    // move key-value pairs to new table
    for kv in this.__table.iter() {
      let hash_val = (this.__hash_func)(this, kv.key()) % size
      table.insert(hash_val, kv)
    }
    // update table
    // NOTE: do not use 'del' method
    //       because it will release all existing pairs
    free(this.__table.table as u8*)
    this.__table = table
    true
  }
  else {
    false
  }
}

// constructor (with parameters)
public def newHashMap(hash_func: HashFunc): HashMap {
  [HashMap] {newTable(TABLE_SIZE), 0 as usize, hash_func}
}

// constructor
public def newHashMap(): HashMap {
  newHashMap(defaultHasher)
}

// destructor
public def del(this: HashMap var&) {
  this.__table.del()
}

inline def size(this: HashMap&): usize {
  this.__size
}

inline def empty(this: HashMap&): bool {
  this.__size == 0 as usize
}

// insert key value pair to hash map
public def insert(this: HashMap var&, key: u32, value: u8 var*): bool {
  var hash_val = this.hash(key)
  if this.__table.get(hash_val, key) == null as u8 var* {
    if this.rehash() {
      hash_val = this.hash(key)
    }
    this.__table.insert(hash_val, key, value)
    this.__size += 1 as usize
    true
  }
  else {
    false
  }
}

// get value by key
public def get(this: HashMap var&, key: u32): u8 var* {
  let hash_val = this.hash(key)
  this.__table.get(hash_val, key)
}

// remove value by key
public def remove(this: HashMap var&, key: u32): bool {
  let hash_val = this.hash(key)
  let ret = this.__table.remove(hash_val, key)
  if ret {
    this.__size -= 1 as usize
  }
  ret
}

// remove all key-value pairs
public def clear(this: HashMap var&) {
  this.__table.del()
  this.__table = newTable(TABLE_SIZE)
  this.__size = 0 as usize
}

// get iterator
public def iter(this: HashMap&): __KvPairTableIter {
  this.__table.iter()
}
